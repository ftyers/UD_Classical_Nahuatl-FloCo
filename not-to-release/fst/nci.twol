Alphabet
 
 A B C D E F G H I J K L M N O P Q R S T U W X Y Z
 a b c d e f g h i j k l m n o p q r s t u w x y z

 %{a%}:a
 %{q%}:q %{q%}:c
 %{n%}:n %{n%}:m
 %{i%}:i %{u%}:u
 %{u%}:0
 %{N%}:n %{N%}:m
 %{o%}:o %{o%}:0
 %{i%}:0
 %{T%}:t
! h:0

 %~:%~
 %·:%·
 %>:%>
 %«:%« 
 %»:%»

;


Sets

Vow   = a e i o ;

Cns   = b c d f g h j k l m n p q r s t w x y z u ;


Rules

"Reflexive mo"
o:0 <=> m _ %>: %{i%}: ;
        m _ %>: e Cns ; 
        m _ %>: i Cns ; 

!"Reduplication glottal"
!h:h <=> _ ;
!    except 
!        .#. Cx: Vx: _ Cx: Vx: ;
!        where Cx in Cns 
!		Vx in Vow ;
!

"hu/uh I"
%{u%}:0 <=> %{u%}: h _ %>: [Cns | .#.] ;

!@ c:c a:a {u}:u h:h {u}:0 q:q u:u e:e

"hu/uh II"
%{u%}:0 <=> _ h %{u%}: %>: Vow ;

"hui/hua -> uh (I)"
h:u <=> _ u: [ a: | i: ] %>: %{T%}: ;

"hui/hua -> uh (II)"
u:h <=> h: _ [ a: | i: ] %>: %{T%}: ;

!@ o {N}:m > p o l i h:u u:h i:0 > {T}:t i u h

"uh + huan (I)"
u:0 <=> _ h: %>: h u a n ;

!@ i>teiccauh>huan

"uh + huan (II)"
h:0 <=> u: _ %>: h u a n ;

!@ i>teiccauh>huan

!@ c:c a:a {u}:0 h:h {u}:u a:a

"Objeto I"
u:0 <=> %{q%}:c _  ; 

"Objeto II"
i:0 <=> %{q%}:c u:0 _ %>: ;
                    _ %>: %{i%}: ; 
                    _ %>: %{a%}: ;

!@ {q}:q u:u i:0 >:0 {i}:i h:h t:t o:o a:a >:0 y:y a:a

"c/qu I"
u:0 <=> _ .#.  ;
        _ %>: q u e ;

"c/qu I"
q:c <=> _ u:0 .#.  ;
        _ u:0 %>: q u e ;

"i ~ e"
i:0 => %{q%}: u _ %>: e ; 
	except 
		%{q%}:c _ ;
		

!@ {q}:q u:u i:0 >:> e:e l:l l:l a:a c:c u:u a:a h:h u:u a:a

"removal of -a before ligature"
Vx:0 <=> o _ %>: t i ·: ; 
	where Vx in ( a ) ;

!@ {q}:q u > t e t e m o a:0 > t i · n e m i

"removal of vow before -tiuh"
Vx:0 <=> [ Cns | i | o | :u :h ] _ %>: %{T%}: ; 
	where Vx in ( a i ) ;

!@ c h a c h a p a n i:0 > {T}:t i u h

!@ n i > c > t l a t i a:0 > {T}:t i u h

!@ m o > m i m i l o a:0 > {T}:t i u h 

!@ t i > m o > m a t i > {T}:t o q u e

"m>n before {T}"
m:n <=> _ i:0 %>: %{T%}: ; 


!@ t e h t e m:n i:0 > {T}:t i u h


